plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

def projectName = '_github_maamissiniva_bitmap-font-descriptor'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))
eclipse.project.name = projectName
jar.archiveBaseName  = 'maamissiniva-bitmap-font-descriptor'

group   = 'io.github.maamissiniva'
version = '0.1.0'

jar {
    manifest {
        attributes('Implementation-Title':   project.name,
                   'Implementation-Version': project.version)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'maamissiniva-bitmap-font-descriptor'

            from components.java

            pom {
                name = 'maamissiniva-bitmap-font-descriptor'
                description = 'Bitmap font descritptor'
                url = 'https://github.com/maamissiniva/bitmap-font-descriptor'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'LGPL-2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'maamissiniva'
                        name = 'Maamissiniva'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/maamissiniva/bitmap-font-descriptor.git'
                    developerConnection = 'scm:git:ssh://github.com/maamissiniva/bitmap-font-descriptor.git'
                    url = 'https://github.com/maamissiniva/bitmap-font-descriptor'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
